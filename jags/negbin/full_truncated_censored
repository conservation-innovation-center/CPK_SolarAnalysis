
  model{
    #Likelihood
    
    # Distribution Parameters for Random Efects on
    # 1. Negative Binomial probability p ~ dbeta(shape1, shape2)
    p_alpha_shp2 <- p_alpha_shp1*((1/p_alpha_mu) - 1)
    p_alpha_shp1 <- (((1-p_alpha_mu)/p_alpha_v)-1/p_alpha_mu )* pow(p_alpha_mu, 2)
    
    # random intercept per state on binomial intercept and neg binomial intercept
    for (s in 1:S){
      p_alpha[s] ~ dbeta(p_alpha_shp1, p_alpha_shp2)
    }
    
    # for every observation
    for (i in 1:N){
      # time to detection is a negative binomial process with state-specific shape and rate determined by covariates
      ttd[i] ~ dnegbin(p[i], 1)T(l[i],)
      logit(p[i]) <- logit(p_alpha[statei[i]]) + p_beta_impervious16*impervious16[i] + p_beta_open16*open16[i] + p_beta_tree_cover16*tree_cover16[i] + p_beta_cultivated16*cultivated16[i] + p_beta_slope*slope[i] + p_beta_GAP_Sts*GAP_Sts[i] + p_beta_line_dist*line_dist[i] + p_beta_road_dist*road_dist[i] + p_beta_POPULATION*POPULATION[i] + p_beta_lat*lat[i]
      
      # model for censoring observed arrays due to not seeing into the future
      # whether we see an array is a bernouli process determined by
      # theta is 0 if site will never be developed (i.e. z[i] = 0) 
      #  or will be developed but not detected yet (i.e. z[i] = 1, ttd[i] > Tmax[i])
      
      d[i]~dinterval(ttd[i], tmax[i])
      
      # Expected data under current model
      ttd_sim[i] ~ dnegbin(p[i], 1)
      ttd_exp[i] <- (1-p[i])/p[i]
      chi2[i] <- pow((ttd[i] - ttd_exp[i]), 2)/ttd_exp[i]
      chi2_sim[i] <- pow((ttd_sim[i] - ttd_exp[i]), 2)/ttd_exp[i]
    }
    
    fit <- mean(chi2[])
    fit_sim <- mean(chi2_sim[])
    bpv <- step(fit_sim - fit)
    
    #Priors
    # we want the mean of the gamma dist on weibull shape to be 1 and variance 1000
    # to simulate gamma(0.0001, 0.0001) with no state effect
    p_alpha_mu ~ dunif(0, 1)
    p_alpha_v ~ dunif(0, 0.2)
    # psi_alpha_mu ~ dunif(0, 1) 
    # psi_alpha_v ~ dunif(0, 0.2)
    # psi ~ dunif(0,1)
    p_beta_impervious16~dnorm(0, 0.0001)
    p_beta_open16~dnorm(0, 0.0001)
    p_beta_tree_cover16~dnorm(0, 0.0001)
    p_beta_cultivated16~dnorm(0, 0.0001)
    p_beta_slope~dnorm(0, 0.0001)
    p_beta_GAP_Sts~dnorm(0, 0.0001)
    p_beta_line_dist~dnorm(0, 0.0001)
    p_beta_road_dist~dnorm(0, 0.0001)
    p_beta_POPULATION~dnorm(0, 0.0001)
    p_beta_lat~dnorm(0, 0.0001)
  }
  